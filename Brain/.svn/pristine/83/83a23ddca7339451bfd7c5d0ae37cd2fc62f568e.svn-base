package kr.pe.hs.tween
{
	import flash.utils.clearTimeout;
	import flash.utils.setTimeout;
	
	import mx.effects.Tween;

	public class Tween
	{
		private var _tween:mx.effects.Tween;
		private var _listener:Object;
		private var _delayTimer:Number;
		private var _isFinish:Boolean;
		
		private var _startValue:Number;
		private var _endValue:Number;
		private var _delay:Number;
		private var _duration:Number;
		private var _callback:Function;
		
		public function Tween(startValue:Number,endValue:Number,delay:Number,duration:Number,callback:Function)
		{
			_isFinish=false;
			
			_startValue=startValue;
			_endValue=endValue;
			_delay=delay;
			_duration=duration;
			_callback=callback;
			
			_listener={
				onTweenUpdate:function(value:Number):void{
					if(!_isFinish){
						_callback(value,false);
					}
				},
				onTweenEnd:function(value:Number):void{
					if(!_isFinish){
						_isFinish=true;
						
						_listener=null;
						_tween=null;
						
						_callback(value,true);
						_callback=null;
					}
				}
			}
			
			_delayTimer=NaN;
			if(_delay){
				_delayTimer=setTimeout(startTween,_delay);
			}
			else{
				startTween();
			}
		}
		private function startTween():void{
			if(!isNaN(_delayTimer)) clearTimeout(_delayTimer);
			_delayTimer=NaN;
			
			_tween=new mx.effects.Tween(_listener,_startValue,_endValue,_duration);
		}
		
		public function endTween():void{
			if(_tween) _tween.endTween();
			
			if(!isNaN(_delayTimer)) clearTimeout(_delayTimer);
			_delayTimer=NaN;
			_callback=null;
			_listener=null;
			_tween=null;
		}
		
	}
}